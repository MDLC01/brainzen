#RESET = "\x1b[0m";
#RED = "\x1b[91m";

func swap(char[16] arr, char k, char l) -> char[16] {
    let xk: char;
    let xl: char;
    for (e : arr, i : 0..15) {
        if (i == k) xk = e;
        if (i == l) xl = e;
    }
    for (e : arr, i : 0..15) {
        if (i == k) e = xl;
        if (i == l) e = xk;
    }
    return arr;
}

func sort(char[16] arr) -> char[16] {
    for (start : 0..15) {
        let min_index = 0;
        let min_value = 0xff;
        for (e : arr, i : 0..15) {
            if (i >= start) {
                if (e <= min_value) {
                    min_index = i;
                    min_value = e;
                }
            }
        }
        arr = swap(arr, start, min_index);
    }
    return arr;
}

proc test(char[16] arr) {
    print("Initial array: ");
    log(arr);
    print("Sorted array:  ");
    log(sort(arr));
}

proc main() {
    test([5, 137, 2, 74, 7, 69, 0, 102, 254, 3, 33, 255, 9, 17, 7, 191]);
    test([16, 15, 14, 13, 12, 11, 10, 9, 8, 7, 6, 5, 4, 3, 2, 1]);
    test([1, 2, 3, 4, 5, 6, 7, 8, 9, 10, 11, 12, 13, 14, 15, 16]);
}
